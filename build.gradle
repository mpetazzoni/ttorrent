buildscript {
	apply from: file('gradle/buildscript.gradle'), to: buildscript 
}

apply plugin: 'org.anarres.stdproject'
stdproject {
	javadocLink "http://netty.io/4.0/api/"

	javadocGroup "BitTorrent Protocols", "com.turn.ttorrent.protocol*"
	javadocGroup "BitTorrent Client Implementation", "com.turn.ttorrent.client*"
	javadocGroup "BitTorrent Tracker Implementations", "com.turn.ttorrent.tracker*"
}

subprojects {
	group = "org.anarres.ttorrent"

	apply plugin: 'org.anarres.stdmodule'
	stdmodule {
		description "An embeddable high performance bittorrent library, client and tracker."
		author id: 'shevek', name: 'Shevek', email: 'github@anarres.org'
		license 'Apache-2.0'
	}

	apply plugin: 'eclipse'
	eclipse {
		classpath {
			downloadSources = true
		}
		jdt {
			sourceCompatibility = 1.7
			targetCompatibility = 1.7
		}
	}

	configurations {
		compile {
			exclude group: 'commons-logging', module: 'commons-logging'
			// exclude group: 'log4j', module: 'log4j'
			// exclude group: 'org.slf4j', module: 'slf4j-log4j12'
		}
	}

	dependencies {
		testCompile 'org.easymock:easymock:3.2'

		testRuntime 'org.slf4j:jcl-over-slf4j:1.7.12'
	}

	sourceCompatibility = 1.7
	animalsniffer {
		signature = "org.codehaus.mojo.signature:java17:+@signature"
	}
}

project(':ttorrent-protocol') {
	dependencies {
		compile 'com.google.code.findbugs:annotations:2.0.3'
		compile 'org.slf4j:slf4j-api:1.7.7'
		compile 'com.google.guava:guava:18.0'
		compile 'io.dropwizard.metrics:metrics-core:3.1.0'
		compile 'io.netty:netty-all:4.0.23.Final'

		testCompile project(':ttorrent-tracker-simple')
		testCompile 'commons-io:commons-io:2.4'
	}
}

project(':ttorrent-tracker-client') {
	dependencies {
		compile project(':ttorrent-protocol')
		compile 'org.apache.httpcomponents:httpasyncclient:4.0.2'

		testCompile project(':ttorrent-tracker-simple')
	}
}

project(':ttorrent-tracker') {
	dependencies {
		compile project(':ttorrent-protocol')

		testCompile project(':ttorrent-tracker-simple')
	}
}

project(':ttorrent-tracker-simple') {
	apply plugin: 'application'

	mainClassName = 'com.turn.ttorrent.tracker.simple.SimpleTrackerMain'

	dependencies {
		compile project(':ttorrent-tracker')

		compile 'org.simpleframework:simple:5.1.6'
		compile 'net.sf.jopt-simple:jopt-simple:4.7'

		testCompile project(':ttorrent-tracker-client')
	}
}

project(':ttorrent-tracker-servlet') {
	dependencies {
		compile project(':ttorrent-tracker')

		compile 'javax.servlet:javax.servlet-api:3.1.0'
	}
}

project(':ttorrent-tracker-spring') {
	dependencies {
		compile project(':ttorrent-tracker-servlet')

		def springVersion = "4.1.1.RELEASE"
		compile "org.springframework:spring-webmvc:$springVersion"

		// compile 'javax.servlet:servlet-api:2.5'
		compile 'javax.servlet:javax.servlet-api:3.1.0'
	}
}

project(':ttorrent-client') {
	apply plugin: 'application'

	mainClassName = 'com.turn.ttorrent.client.main.ClientMain'

	dependencies {
		compile project(':ttorrent-tracker-client')

		compile 'commons-io:commons-io:2.4'
		compile 'net.sf.jopt-simple:jopt-simple:4.7'

		testCompile project(':ttorrent-tracker-simple')
		testCompile project(':ttorrent-protocol').sourceSets.test.output
		testCompile project(':ttorrent-tracker-client').sourceSets.test.output
	}
}
