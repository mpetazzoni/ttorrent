// GRADLE-2087 workaround, perform after java plugin
status = project.hasProperty('preferredStatus')?project.preferredStatus:(version.contains('SNAPSHOT')?'snapshot':'release')

subprojects { project ->
	apply plugin: 'java' // Plugin as major conventions
	apply plugin: 'errorprone'
	apply plugin: 'info'

	sourceCompatibility = 1.5

	// Restore status after Java plugin
	status = rootProject.status

	repositories {
		// mavenLocal()
		mavenCentral()
		// jcenter()
	}

	dependencies {
		compile 'com.google.code.findbugs:jsr305:2.0.2'
		compile 'org.slf4j:slf4j-api:1.7.7'

		testCompile 'junit:junit:4.11'

		testRuntime 'org.slf4j:slf4j-simple:1.7.7'
	}

	tasks.withType(JavaCompile) {
		// options.incremental = true
	}

	javadoc {
		options.links "http://docs.oracle.com/javase/7/docs/api/",
			"http://docs.oracle.com/javaee/7/api/",
			"http://docs.guava-libraries.googlecode.com/git/javadoc/"
	}

	test {
		systemProperty 'org.apache.commons.logging.Log', 'org.apache.commons.logging.impl.SimpleLog'
		systemProperty 'org.apache.commons.logging.simplelog.defaultlog', 'debug'
		systemProperty 'org.apache.commons.logging.diagnostics.dest', 'STDERR'
		systemProperty 'org.slf4j.simpleLogger.logFile', 'System.out'
		systemProperty 'org.slf4j.simpleLogger.defaultLogLevel', 'debug'
		systemProperty 'jna.nosys', 'true'

		testLogging {
			if (System.properties['test.single']) {
				// events "passed", "skipped", "failed"
				events "started", "passed", "skipped", "failed"
				showExceptions true
				exceptionFormat "full"
				showStandardStreams true
			} else {
				events "started", "skipped", "failed"
			}

			debug {
				events "started", "passed", "skipped", "failed", "standard_out", "standard_error"
				exceptionFormat "full"
			}
		}
	}
}

apply plugin: 'build-announcements'
apply plugin: 'build-dashboard'

// Generate wrapper, which is distributed as part of source to alleviate the need of installing gradle
wrapper {
	gradleVersion = '2.2.1'
}
